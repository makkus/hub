buildscript {
    repositories {
        mavenLocal()
        maven { url "http://repos.zeroturnaround.com/nexus/content/groups/zt-public/" }
    }
    dependencies {
        classpath group: 'org.zeroturnaround', name: 'gradle-jrebel-plugin', version: '1.1.2'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

apply plugin:'idea'

ext {
    springVersion = '4.0.0.RELEASE'
    springDataCommonsVersion = '1.7.1.RELEASE'
}


version = '0.1-SNAPSHOT'
group = 'hub'

subprojects {

    repositories {
        mavenLocal()
        mavenCentral()
    }
    apply plugin: 'java'
    apply plugin: 'rebel'
    apply plugin: 'maven-publish'

    version = '0.1-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    configurations {
        markdownDoclet
    }

    dependencies {
        markdownDoclet('ch.raffael.pegdown-doclet:pegdown-doclet:1.1.1')
    }

    jar.dependsOn(generateRebel)

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId 'researchHub'
                version version
                from components.java
            }
        }
        repositories {
            maven {
                url "http://myrepo.com/repo" // change to point to your repo, e.g. http://my.org/repo
            }
        }
    }

    javadoc {

        options.docletpath = configurations.markdownDoclet.files.asType(List)
        options.doclet = 'ch.raffael.doclets.pegdown.PegdownDoclet'
        options.addStringOption("extensions", "autolinks,definitions,smartypants,tables,wikilinks,fenced-code-blocks")
        options.links = [
                "http://docs.oracle.com/javase/8/docs/api/",
                "http://docs.guava-libraries.googlecode.com/git-history/v15.0/javadoc/"
        ]
    }
}
